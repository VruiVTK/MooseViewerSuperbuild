# This script fetches and builds VTK

SET(VTK_ROOT
  ${CMAKE_BINARY_DIR}/VTK
  CACHE INTERNAL ""
  )

if(WIN32)
  set(vtk_tbb_libsuffix ${CMAKE_IMPORT_LIBRARY_SUFFIX})
else()
  set(vtk_tbb_libsuffix ${CMAKE_SHARED_LIBRARY_SUFFIX})
endif()

# NOTE: For now we only support Mac and Windows.
SET (VTK_CMAKE_ARGS
  -DBUILD_TESTING:BOOL=OFF
  -DBUILD_SHARED_LIBS:BOOL=OFF
  -DBUILD_EXAMPLES:BOOL=OFF
  -DModule_vtkRenderingExternal:BOOL=ON
  -DVTK_RENDERING_BACKEND:STRING=${VTK_RENDERING_BACKEND}
  -DVTK_SMP_IMPLEMENTATION_TYPE:STRING=TBB
  "-DTBB_ROOT:PATH=${CMAKE_BINARY_DIR}/TBB/bld"
  "-DTBB_INCLUDE_DIR:PATH=${CMAKE_BINARY_DIR}/TBB/bld/include"
  "-DTBB_LIBRARY_RELEASE:FILEPATH=${CMAKE_BINARY_DIR}/TBB/bld/lib/libtbb${vtk_tbb_libsuffix}"
  "-DTBB_LIBRARY_DEBUG:FILEPATH="
  "-DTBB_MALLOC_INCLUDE_DIR:PATH=${CMAKE_BINARY_DIR}/TBB/bld/include"
  "-DTBB_MALLOC_LIBRARY_RELEASE:FILEPATH=${CMAKE_BINARY_DIR}/TBB/bld/lib/libtbbmalloc${vtk_tbb_libsuffix}"
  "-DTBB_MALLOC_LIBRARY_DEBUG:FILEPATH="
  )

ExternalProject_Add(
  VTK
  DEPENDS TBB
  PREFIX "${VTK_ROOT}"
  STAMP_DIR "${VTK_ROOT}/stamp"
  GIT_REPOSITORY "https://gitlab.kitware.com/vtk/vtk.git"
  GIT_TAG "d6dac9974997e9ef0f0dd8ad1c6e498ace65318d"
  #  UPDATE_COMMAND ${GIT_EXECUTABLE} pull
  SOURCE_DIR "${VTK_ROOT}/src"
  BINARY_DIR "${VTK_ROOT}/bld"
  CMAKE_ARGS "${VTK_CMAKE_ARGS}"
  INSTALL_COMMAND ""
  )
